apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.jfrog.artifactory'

version project.property('version')
group 'com.vcoolish'

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.sourceFiles
}

artifacts {
    archives sourcesJar
}

publishing {
    publications {
        Production(MavenPublication) {
            artifact("$buildDir/outputs/aar/swipeoptionlayout-release.aar")
            artifact sourcesJar { 
                classifier "sources"
            }
            groupId this.group
            artifactId 'swipeoptionlayout'
            version this.version

            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')

                configurations.implementation.allDependencies.each {
                    if (it.name != 'unspecified') {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
}

artifactory {
    contextUrl = 'https://oss.jfrog.org'
    resolve {
        repository {
            repoKey = 'libs-release'
        }
    }
    publish {
        repository {
            repoKey = 'oss-release-local'
            username = System.getenv('BINTRAY_USER')
            password = System.getenv('BINTRAY_KEY')
        }
        defaults {
            publications 'Production'
            properties = [ 'bintray.repo': 'vcoolish/swipeoptionlayout', 'bintray.package': 'com.vcoolish.swipeoptionlayout', 'bintray.version': this.version.toString() ]
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publications = ['Production']
    configurations = ['archives']
    override = true
    pkg {
        repo = 'SwipeOptionLayout'
        name = 'swipeoptionlayout'
        description = "Android Kotlin swipe layout library"
        publish = true
        publicDownloadNumbers = true
        licenses = ['MIT']
        vcsUrl = 'https://github.com/vcoolish/swipeoptionlayout.git'
        dryRun = false
        version {
            name = this.version
            desc = "SwipeOptionLayout ${this.version}"
            released = new Date()
            vcsTag = this.version
        }
    }
}